#CLASS {guofu} OPEN

#ACTION {^%1管事对你说：干的不错，好了，你可以去向耶律大爷覆命(task ok)了。}
{
     #variable status 4;
     taskback
}

#ACTION {^%1管事对着你点了点头。}
{
     #if {$status == 3}
     {
          wield $tool;
          work
     };
     #else
     {
          taskback
     }
}

#ACTION {^“啪”的一声一颗仙丹掉到你面前。}
{
     get dan
}

#ACTION {^【 精 气 】%s%d/%s%d}
{
     #if {%2 < 50}
     {
          exert regenerate
     }
}

#ACTION {^【 食 物 】%s%d/%s%d%s【 潜 能 】  %d}
{
     #if {%2 < 50}
     {
          #variable status 9
     };
     #elseif {%6 > $max_qn}
     {
          #variable status 8
     }
}

#ACTION {^你又饥又渴，觉得自己快要死了。}
{
     #variable status 9
}

#ACTION {^你将剩下的%1吃得干干净净。}
{
     drop rice;
     #if {$status == 9}
     {
          serve
     }
}

#ACTION {^你已经吃太饱了，再也塞不下任何东西了。}
{
     #variable status 1;
     #unticker food;
     #3 drop rice;
     #delay 1 {mgo 帐房;#delay 2 {cun 20 silver}}
}

#ACTION {^你已经喝太多了，再也灌不下一滴水}
{
     drop tea
}

#ACTION {^你带的白银不够。}
{
     #delay 1 {mgo 985;#delay 1 job}
}

#ACTION {^你拿出十两白银，存进了银号。}
{
     #delay 3 {mgo 985;#delay 1 job}
}

#ACTION {^你渴得要虚脱了。}
{
     drink shui dai
}

#ACTION {^你现在的精神不太好，没法和别人套瓷。}
{
     #if {$status ==2}
     {
          #delay 5 {tool}
     };
     #else
     {
          #delay 5 job
     }
}

#ACTION {^你的潜能不够学习这么多次了。}
{
     #unticker xuexi;
     food
}

#ACTION {^你饿得头晕眼花，看来要赶快找点吃的了。}
{
     eat gan liang
}

#ACTION {^厨师将热气腾腾的饭菜递到你手中，笑道：你慢用。}
{
     #ticker food {eat rice;drink tea} 1
}

#ACTION {^武修文交给你一把工具。}
{
     #variable status 3;
     mgo $vnum
}

#ACTION {^武修文对着你点了点头。}
{
     #if {$status == 2}
     {
          tool
     };
     #else
     {
          unwield $tool;
          give $tool to wu xiuwen;
          mgo 985;
          #delay 1 {task ok}
     }
}

#ACTION {^然而你今天太累了，无法再进行任何学习了。}
{
     eatdan
}

#ACTION {^耶律齐看着你会心地一笑。}
{
     #switch {$status}
     {
          #case {0}
          {
               hi
          };
          #case {8}
          {
               xuexi
          };
          #case {9}
          {
               food
          };
          #default
          {
               job
          }
     }
}

#ACTION {^耶律齐说道：…嗯…你先到物品房领工具，然后去后院「%1」吧。}
{
     #if {$status == 1}
     {
          #variable status 2;
          #switch {"%1"}
          {
               #case {"打扫马房"}
               {
                    #variable tool {sao zhou};
                    #variable work {sao mafang};
                    #variable vnum 1010
               };
               #case {"锯木头"}
               {
                    #variable tool {ju zi};
                    #variable work {ju mutou};
                    #variable vnum 1011
               };
               #case {"劈柴"}
               {
                    #variable tool {chai dao};
                    #variable work {pi chai};
                    #variable vnum 1007
               };
               #case {"浇菜地"}
               {
                    #variable tool {shui piao};
                    #variable work {jiao shui};
                    #variable vnum 1013
               };
               #case {"锄草"}
               {
                    #variable tool {chu tou};
                    #variable work {chu cao};
                    #variable vnum 1014
               }
          };
          tool
     }
}

#ACTION {^花茶里的花茶已经被喝得一滴也不剩了。}
{
     drop tea;
     #if {$status == 9}
     {
          serve
     }
}

#ALIAS {eatdan}
{
     eat yinxin dan
}

#ALIAS {food}
{
     mgo 1003;
     #delay 2 serve
}

#ALIAS {job}
{
     #variable status 1;
     ask yelv qi about 工作
}

#ALIAS {setlearn}
{
     #variable max_qn %1;
     #variable learn %2
}

#ALIAS {start}
{
     #read map.tt;
     initmap 88;
     n;
     job;
     #variable skill literate;
     #variable max_qn 20000;
     #variable learn 1
}

#ALIAS {stop}
{
     #unticker work;
     #variable status 0
}

#ALIAS {taskback}
{
     todo;
     #unticker work;
     #delay 5 {mgo 1001; #delay 1 {mgo 995}}
}

#ALIAS {todo}
{
     hp;
     #delay 3 {eat wanshou dan}
}

#ALIAS {tool}
{
     mgo 995;
     #delay 1 {ask wu xiuwen about 工具}
}

#ALIAS {work}
{
     #ticker work {$work} 1
}

#ALIAS {xuexi}
{
     #if {$learn == 2}
     {
          mgo 1000;
          #ticker {xuexi} {xue guo $skill 100} 3
     };
     #elseif {$learn == 1}
     {
          mgo 学堂;
          #ticker {xuexi} {xue zhu $skill 100} 3
     };
     #else
     {
          mgo 994;
          #ticker {xuexi} {xue wu $skill 100} 3
     }
}


#CLASS {guofu} CLOSE
