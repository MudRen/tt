#CLASS {quest} OPEN
#NOP {==任务机器人使用说明==
     1. 做好准备工作：包括学习101级的驯兽术，带上雄黄、干粮和水袋
     2. 在个人设置中根据自己的门派修改别名
     3. 在掌门处手动接任务启动任务
     4. 如果只是需要画地图，做好准备工作后输入【maprun】,开始画城市的地图
quest 参数说明：
     type :
          letter    送信
          quest     杀人
          rumor     自由任务
          map       画地图
     status :
          0 - 发呆
          1 - 未接或已完成任务状态
          2 - 已接且未完成任务状态
          3
          4 - 任务失败
          5
          6 - 目标逃跑
          7
          8
          9
rumor 类型说明：
          1 - capture 追杀
          2 - deliver 送货
          3 - explore 寻找物品
          4 - judge 单正
          5 - search 宝物归还
          6 - shen 正负神
          7 - supply
          8 - trace 找人
};

#nop {==============================个人设置==============================};
#nop {===师傅路径===}
#ALIAS {sf}
{
     mgo 3279
}
#nop {===掌门路径===}
#ALIAS {master}
{
     mgo 玄冥峰
}
#nop {===回门指令===}
#ALIAS {home}
{
     rideto lingzhou;
     master
}
#nop {===个人绝招===}
#ALIAS {pfm}
{
     perform dagger.dian
}
#nop {===属性强化===}
#ALIAS {buff}
{
     perform dodge.piao;
}
#nop {===任务完成后准备工作===}
#ALIAS {eatdan}
{
     eat xueqi dan
}
#nop {===武器===}
#VARIABLE         {weapon} {{name}{鹤形笔}{id}{hexing bi}}
#nop {===经验控制===}
#VARIABLE         {max}  {{exp}{220000}{pot}{22000}}
#VARIABLE         {min}  {{exp}{215000}}
#nop {==============================基本动作==============================};
#ACTION {你运功完毕，吐出一口瘀血，脸色看起来好多了。}
{
     fullme;
     exert heal
}

#ACTION {你现在气血充盈，不需要疗伤。}
{
     #if {$quest[type] == "quest"}
     {
          #if {$my[exp] > $max[exp]}
          {
               #tick {fq} {fangqi exp;hp} 3
          };
          #elseif {$my[pot] >= $max[pot]}
          {
               sf;xuexi
          };
          #else
          {
               #switch {$quest[status]}
               {
                    #case {1}
                    {
                         qm
                    };
                    #case {6}
                    {
                         wentao
                    }
               };
          }
     }
}

#ACTION {这里没有这个人。}
{
     #bell
}

#nop {==============================物品奖励==============================};
#ACTION {获得%1需要消耗你%2点门派贡献，你是否愿意(answer Y|N)接受？}
{

}
#nop {==============================危险==============================};
#ACTION {( 你看起来可能有些累了。)}
{
     pfm
}
#ACTION {( 你动作似乎有点不太灵光，但仍然有条不紊。)}
{
     pfm
}
#ACTION {( 你似乎有些疲惫，但是仍然十分有活力。)}
{
     pfm
}
#ACTION {( 你气喘嘘嘘，看起来状况并不太好。)}
{
     ff
}
#ACTION {( 你似乎十分疲惫，看来需要好好休息了。)}
{
     ff
}
#ACTION {^( 你已经陷入半昏迷状态，随时都可能摔倒晕去。)}
{
     ff;
     #zap
}
#ACTION {^( 你看起来已经力不从心了。)}
{
     ff;
     #zap
}
#ACTION {^你的眼前一黑，接著什么也不知道了....}
{
     #zap
}

#nop {==============================rumor==============================};
#ACTION {^你突然想到：现在江湖正值动乱，何不四处走访，也许可提高自己的经验阅历。}
{
     #variable quest[type] rumor;
     #delay 3 wentao
}

#ACTION {通过这次锻炼，你获得了%1点经验、%2点潜能、%3点江湖阅历}
{
     #variable quest[status] 1;
     food;
     #delay 5 wenlaz
}

#ACTION {%1说道：前两天还听人家说过『%2』呢。}
{
     #if {$quest[status] == 1}
     {
          #variable rumor[info] %2;
          wenrr
     }
}

#ACTION {%1说道：你没有听到大家都在议论『%2』吗？}
{
     #if {$quest[status] == 1}
     {
          #variable rumor[info] %2;
          wenrr
     }
}

#ACTION {%1说道：也没什么大事，只是听说过『%2』罢了。}
{
     #if {$quest[status] == 1}
     {
          #variable rumor[info] %2;
          wenrr
     }
}

#ACTION {%1说道：最近正在盛传『%2』这件事情呢！}
{
     #if {$quest[status] == 1}
     {
          #variable rumor[info] %2;
          wenrr
     }
}

#ACTION {%1说道：你可曾听过最近有关『%2』的传闻？}
{
     #if {$quest[status] == 1}
     {
          #variable rumor[info] %2;
          wenrr
     }
}

#ACTION {%1说道：据说提督府的%2急需一批%3。嘿！你说他想干什么？}
{
     #delay 1 wenxr
}

#ACTION {%1说道：倒是最近听来往的商人说起『%2』的事来。}
{
     wenxr
}

#ACTION {%1说道：嗯，稍等啊，就好…好了，你刚才说啥？}
{
     #if {"$quest[type]" == "rumor"}
     {
          #variable quest[status] 1;
          wenrr
     }
}

#ACTION {%1说道：就好…就好…好了，你说啥？}
{
     #if {"$quest[type]" == "rumor"}
     {
          #variable quest[status] 1;
          wenrr
     }
}

#ACTION {%1说道：据说官府正在捉拿飞贼%2，这家伙，可不是好人啊。}
{
     #delay 2 wenxr
}

#ACTION {%1说道：唉呀呀，听说%2那家伙不知怎么搞的，居然把江湖上六大门派全都给惹了}
{
     #if {$quest[status] == 2}
     {
          #variable target[name] %2;
          #variable rumor[target] %2;
          #echo wenrr;
          wenxr
     }
}

#ACTION {%1说道：哦，听说最近六大门派已经上%2找%3去了，我看%3这次是小命不保罗。}
{
     #if {$quest[status] == 2}
     {
          #variable target[place] %2;
          #echo questrun;
     }
}

#ACTION {%1说道：听说%2丢了%3，有人说不知怎么的就落到了%4手中了。}
{
     #if {$quest[status] == 2}
     {
          #variable rumor[to] %2;
          #variable rumor[item] %3;
          #variable rumor[from] %4;
          #variable rumor[target] %4;
          wenrr
     }
}

#ACTION {%1说道：哦，%2呀，据说正在%3，大家都奔%2去了，看来%2不妙噢！}
{
     #if {$quest[status] == 2}
     {
          #variable rumor[from_place] %3;
          #variable rumor[target] $rumor[to];
          wenrr
     }
}

#ACTION {%1说道：哦，%2呀，据说正在%3，找%2的$rumor[item]呢！}
{
     #if {$quest[status] == 2}
     {
          #variable rumor[to_place] %3;
          #variable target[name] $rumor[from];
          #variable target[place] $rumor[from_place];
          questrun
     }
}

#ACTION {$rumor[from]把$rumor[item]交给了你。}
{
     i;
     #variable target[name] $rumor[to];
     #variable target[place] $rumor[to_place];
     #delay 2 questrun
}

#ACTION {%1说道：听说%2和%3在%4发生了纠纷，不知道为什么，要是单正在就好了。}
{
     wenxr
}

#ACTION {%1说道：听说%2和%3在%4被仇家围追堵杀，双方火拼了几个时辰}
{
     wenxr
}

#ALIAS {wenfr}
{
     ask %1 about rumor
}

#nop {==============================quest==============================};
#ACTION {$master[name](%1)}
{
     #variable master[id] %1
}

#ACTION {^这里没有这个人，你怎么领任务？}
{
     #bell
}

#ACTION {^%1对你说道：我这里暂时也没什么事情，你还是自己锻炼一段时间吧。}
{
     #variable master[name] %1;
     food;
}

#ACTION {通过这次锻炼你获得了%1点经验，%2点潜能及%3点实战体会。}
{
     ready
}

#ACTION {^据说此人前不久曾经在%1出没。}
{
     #if {$quest[status] < 3}
     {
          #variable target[place] %1
     }

}

#ALIAS {qm}
{
     #variable quest[status] 1;
     #variable quest[type] quest;
     quest $master[id]
}

#ALIAS {questok}
{
     fullme;
     #delay 1 {home;#delay 2 {finish}}
}

#ALIAS {finish}
{
     #variable quest[status] 1;
     give head to $master[id]
}

#ALIAS {questrun}
{
     #path load ${path$placetable[$target[place]][path]};
     #variable pathrun 1;
     #variable pathend 0;
     rideto $placetable[$target[place]][id];
     #map goto $placetable[$target[place]][vnum];
     #switch {"$quest[type]"}
     {
          #case {"letter"} {#path run 0.3};
          #case {"quest"} {#path run 0.3};
          #case {"rumor"} {#path run 0.3};
          #case {"map"} {map here};
     }
}

#ALIAS {start}
{
     ok;
     #if {$quest[status] == 6}
     {
          #delay 2 wentao
     };
     #elseif {$quest[status] == 2}
     {
          #delay 2 questrun
     };
     #elseif {$quest[status] == 1}
     {
          home;quest
     }
}

#ALIAS {ready}
{
     #if {$quest[status] == 0}
     {
          bfd;
          #delay 2 {bxh;#delay 1 home}
     };
     #else
     {
          eatdan;
          #delay 5 {exert heal}
     }
}

#ALIAS {todo}
{
     #variable pathrun 0;
     #path destroy;
     buff;
     #if {"$quest[type]" == "letter"}
     {
          give letter to $target[id]
     };
     #elseif {"$quest[type]" == "quest"}
     {
          kill $target[id]
     };
     #elseif {"$quest[type]" == "rumor"}
     {
          #if {"$target[name]" == "$rumor[from]"}
          {
               ask $target[id] about $rumor[item]
          };
          #elseif {"$target[name]" == "$rumor[to]"}
          {
               give $rumor[itemid] to $target[id]
          }
     }
}

#ALIAS {qinfo}
{
     #variable quest;
     #variable target;
     #variable rumor
}

#nop {==============================送信==============================};
#ACTION {^%1交给你一张回执。}
{
     fullme;
     rideto gc;
     #delay 3 {home;#delay 2 {finish}}
}

#ACTION {^%1吩咐你在%2月%3日之前把信件送到%4(%5)手中，取回执交差。}
{
     #variable master[name] %1;
     #variable target[name] %4;
     #variable target[id] %5
}

#ACTION {^%1对你道：这封信你帮我交到%2手中，他现在应该在%3，然后把回执带回来给我！}
{
     #variable quest[type] letter;
     #variable quest[status] 2;
     #variable master[name] %1;
     #variable target[name] %2;
     #variable target[place] %3;
     quest;
     #delay 2 {questrun}
}

#nop {==============================杀人==============================};
#ACTION {^%1吩咐你在%2之前割下%3(%4)的人头，回%5交差。}
{
     #variable master[name] %1;
     #variable target[name] %3;
     #variable target[id] %4;
     #variable quest[type] quest
}

#ACTION {^%1对你道：%2这个所谓大侠屡次和我派作对，听说%3最近在%4，你去宰了%3，提头来见！}
{
     #variable master[name] %1;
     #variable target[name] %2;
     #variable target[place] %4;
     #variable quest[type] quest;
     #variable quest[status] 2;
     quest;
     #delay 3 start;
}

#ACTION {^$master[name]呆了半晌，这才对你叹道：这事麻烦了，看来只有靠你自己努力了。}
{
     #variable quest[status] 6;
}

#ACTION {^$target[name]扑在地上挣扎了几下，腿一伸，口中喷出几口鲜血，死了！}
{
     cut head from corpse;
     get silver from corpse;
     fullme;
     rideto gc;
     #delay 3 {home;#delay 2 {finish}}
}

#nop {==============================任务行动==============================};
#ACTION {^$target[name]说道：哼！找死！}
{
     pfm
}

#ACTION {某人：听说$target[name]隐居到了深山老泽，从此不再出没在世间。}
{
     #bell;faint;
     #variable quest[status] 4;
}

#nop {==============================目标逃跑==============================};
#ACTION {$target[name]莫慌，我来也！}
{
     #variable quest[status] 6;
     ready
}

#ACTION {在你一阵狂攻之下，$target[name]只有招架之功，哪里还有还手之力？}
{
     #variable quest[status] 6;
     ready
}

#ACTION {仅这一霎，$target[name]身形一转，飘然退去}
{
     #variable quest[status] 6;
     ready
}

#ACTION {只见眼前一阵红雾，待得雾散去，哪里还有$target[name]的影子}
{
     #variable quest[status] 6;
     ready
}

#ACTION {$target[name]身负重伤，已然不敌，悲愤间只放声疾呼，一时震得你耳鼓欲破}
{
     #variable quest[status] 6;
     ready
}

#ACTION {你略一疏神，已被$target[name]连环数招逼退，只见$target[name]迈开大步，逃遁而走。}
{
     #variable quest[status] 6;
     ready
}

#ACTION {$target[name]晃了两下，忽然跌倒。你收住招式，笑道：“$target[name]，你……”}
{
     #variable quest[status] 6;
     ready
}

#nop {==============================寻找目标==============================};
#ACTION {^%1说道：这事我也不太清楚，不过听说他好像在%2。}
{
     #if {$quest[status] == 6}
     {
          #variable target[place] %2;
          #delay 1 questrun;
     }
}
{1}

#ACTION {^%1说道：不知道……哦？好像听人说过是在%2。}
{
      #if {$quest[status] == 6}
     {
          #variable target[place] %2;
          #delay 1 questrun;
     }
}

#ACTION {%1说道：听说$target[name]被人打伤逃走，据说是躲到%2去了。}
{
     #variable target[place] %2;
     #delay 1 questrun;
}

#ACTION {%1说道：听说$target[name]听到了江湖上有人追杀的风声，已经躲到%2去了。}
{
     #if {$quest[status] == 6}
     {
          #if {"%3" == "很远的地方"}
          {
               #delay 1 wenchou
          };
          #else
          {
               #variable target[place] %2;
               #delay 1 questrun;
          }
     }
}

#ACTION {%1说道：听说$target[name]被人打死了吧？咦？不过听人说在%2也见过%3呀！}
{
     #variable target[place] %2;
     #delay 1 questrun;
}

#ACTION {^%1说道：听说$target[name]和人动手，差点被打死，不过最后还是侥幸逃走了，好像去了%2。}
{
     #variable target[place] %2;
     #delay 1 questrun;
}

#ACTION {%1说道：有人说$target[name]就在咱们这地界呢，但是也有人说在%2，不知道是真是假。}
{
     #variable target[place] %2;
     #delay 1 questrun;
}

#ACTION {%1说道：$target[name]呀，有人说在%2看到过%3，好像还受伤了。}
{
     #variable target[place] %2;
     #delay 1 questrun;
}

#ACTION {%1说道：$target[name]听说是在%2啊，不过前一阵好像来过这里的。}
{
     #variable target[place] %2;
     #delay 1 questrun;
}

#ACTION {^%1说道：你找%2干啥？%2不是在%3么？}
{
     #if {$quest[status] == 6}
     {
          #if {"%3" == "很远的地方"}
          {
               #delay 1 wenchou
          };
          #else
          {
               #variable target[place] %3;
               #delay 1 questrun;
          }
     }
}

#ACTION {^北丑皱了皱眉头，对你摇摇头道：看来你这次确实是遇到了困难，收你十两白银就是了。}
{
     give 10 silver to bei chou
}

#ACTION {^北丑在你的耳边悄声说道：据可靠消息，这个人刚才在%1。}
{
     #variable target[where] %1;
     gps %1
}

#ALIAS {wenchou}
{
     rideto gc;
     mgo 137;
     #delay 2 {ask bei chou about $target[id]}
}

#ALIAS {wt}
{
     ask %1 about $target[name]

}

#ALIAS {wentao}
{
     wen${placetable[$target[place]][path]};
}

#ALIAS {wenrr}
{
     #if {$quest[status] == 1}
     {
          #variable quest[status] 2;
          ask $rumor[knower] about $rumor[info]
     };
     #elseif {$quest[status] == 2}
     {
          ask $rumor[knower] about $rumor[target]
     }
}

#ALIAS {wenxr}
{
     #if {"$quest[type]" == "quest"}
     {
          ask $rumor[knower] about $target[name]
     };
     #else
     {
          #variable quest[status] 1;
          #variable quest[type] rumor;
          ask $rumor[knower] about rumor
     }
}

#ALIAS {wenyz}
{
     mgo 124;
     #variable rumor[knower] {aqing sao};
     #delay 2 wenxr
}
#ALIAS {wenyz2}
{
     mgo 276;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenhs}
{
     mgo 293;
     #variable rumor[knower] {fortune teller};
     #delay 2 wenxr
}

#ALIAS {wenjz}
{
     mgo 456;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wensz}
{
     mgo 506;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenzz}
{
     mgo 607;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenly}
{
     mgo 781;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenxy}
{
     mgo 908;
     #variable rumor[knower] {seller};
     #delay 2 wenxr
}
#ALIAS {wenxy2}
{
     mgo 952;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenfz}
{
     mgo 1100;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenqz}
{
     mgo 1110;
     #variable rumor[knower] {chuan fu};
     #delay 2 wenxr
}

#ALIAS {wenfs}
{
     mgo 1143;
     #variable rumor[knower] {feng qi};
     #delay 2 wenxr
}

#ALIAS {wennh}
{
     mgo 1157;
     #variable rumor[knower] {yu fu};
     #delay 2 wenxr
}

#ALIAS {wenca}
{
     mgo 1457;
     #variable rumor[knower] {xiaoer};
     #delay 2 wenxr
}
#ALIAS {wenca2}
{
     mgo 1459;
     #variable rumor[knower] {xiaoer};
     #delay 2 wenxr
}

#ALIAS {wenhz}
{
     mgo 1580;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenlaz}
{
     mgo 1652;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenxx}
{
     mgo 1688;
     #variable rumor[knower] {bosi shangren};
     #delay 2 wenxr
}

#ALIAS {wents}
{
     mgo 1703;
     #variable rumor[knower] {jiuyi daoren};
     #delay 2 wenxr
}

#ALIAS {wencd}
{
     mgo 2068;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenlx}
{
     mgo 2103;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenbj}
{
     mgo 2195;
     #variable rumor[knower] {chen laotou};
     #delay 2 wenxr
}

#ALIAS {wenbj}
{
     mgo 2370;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenss}
{
     mgo 2404;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenkf}
{
     mgo 2536;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenwg}
{
     mgo 2605;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}
#ALIAS {wenzn}
{
     mgo 2605;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenkm}
{
     mgo 2758;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wengw}
{
     mgo 3166;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#ALIAS {wenliz}
{
     mgo 3227;
     #variable rumor[knower] {xiao er};
     #delay 2 wenxr
}

#nop {==============================发现目标==============================};
#ACTION {^$target[name]%*走了过来。}
{
     l
}

#ACTION {$target[name]正坐在地下修炼内力。}
{
     todo
}

#ACTION {$target[name]一眼瞥见你，「哼」的一声冲了过来！}
{
     todo
}

#ACTION {$target[name]和你一碰面，二话不说就打了起来！}
{
     todo
}

#ACTION {^%s$target[name](%1)}
{
     #if {"$target[name]" == "$rumor[item]"}
     {
          #variable rumor[itemid] %1
     };
     #else
     {
          #variable target[id] %1;
          todo;
          follow %1
     }
}

#ACTION {^你决定开始跟随$target[name]一起行动。}
{
     grin
}

#ACTION {^$target[name]往%1离开。}
{
     #switch {"%1"}
     {
          #case {"北方"} {n};
          #case {"南方"} {s};
          #case {"西方"} {w};
          #case {"东方"} {e};
     }

}
#nop {==============================map here==============================};
#ACTION {你精心的绘制了%1附近的地形。}
{
     #if {"$quest[type]" == "map"}
     {
          #delay 3 {maphere}
     }
}

#ACTION {你已经绘制过这附近的地图了，没有必要再重复了。}
{
     #if {"$quest[type]" == "map"}
     {
          #if {$pathend == 1}
          {
               #bell
          };
          #else
          {
               #delay 0.1 {maphere}
          }
     }
}

#ACTION {你觉得这里没什么好画的。}
{
     #if {"$quest[type]" == "map"}
     {
          #delay 0.1 {maphere}
     }
}

#ACTION {只有在户外才有必要绘制地图。}
{
     #if {"$quest[type]" == "map"}
     {
          #delay 0.1 {maphere}
     }
}

#ALIAS {maphere}
{
     walk;
     #if {$inboat == 1}
     {
          #delay 5 {map here}
     };
     #else
     {
          #delay 0.1 {map here}
     }
}

#ALIAS {maprun}
{
     #variable quest[type] map;
     #if {"%1"==""}
     {
          #math city {$city+1};
          maprun $placelist[$city]

     };
     #else
     {
          #variable target[place] %1;
          questrun
     }
}

#nop {==============================学习技能==============================};
#ACTION {^你的潜能不够学习这么多次了。}
{
     #bell;
     #untick xx
}

#ACTION {^你的%1水平有限，无法领会更高深的%2。}
{
     #bell;
     #untick xx
}

#ACTION {^也许是缺乏实战经验，你对%1的回答总是无法领会。}
{
     #bell;
     #untick xx
}

#ALIAS {xuexi}
{
     #tick xx {xue $sf $sk 100;eatdan} 3
}

#nop {==============================路径处理==============================};
#ACTION {你掂量了一下，觉得自己的轻功似乎还没练到家。}
{
     #if {$pathrun == 1} {#path stop;yell boat} {yell boat};
     #ALIAS river {#if {$pathrun == 1} {#path stop;yell boat} {yell boat}}
}

#ACTION {你鼓足中气，高喊一声：「船家」声音远远传了出去。}
{

}

#ACTION {^渡船(boat)}
{
     #variable inboat 1
}

#ACTION {艄公说道：「到啦，上岸吧。」随即把一块踏脚板搭上堤岸。}
{
     #if {$inboat == 1}
     {
          #variable inboat 0;

     };
     #if {$pathrun == 1}
     {
          #path run 0.3
     }
}

#ACTION {明教五散人「冷面先生」冷谦(leng qian)}
{
     unwield $weapon[id]
}

#nop {==============================其他触发==============================};
#ACTION {【 经 验 】  %1}
{
     #variable my[exp] %1;
     #if {%1 <= min[exp]}
     {
          #untick fq
     }
}

#ACTION {【 潜 能 】  %1}
{
     #variable my[pot] %1;
}

#ACTION {“啪”的一声一颗仙丹掉到你面前。}
{
     get dan;store dan
}

#ACTION {^你渴得要虚脱了。}
{
     drink shui dai
}

#ACTION {^你饿得头晕眼花，看来要赶快找点吃的了。}
{
     eat gan liang
}

#ACTION {^你又饥又渴，觉得自己快要死了。}
{
     food
}

#nop {==============================别名和变量==============================};
#nop {===常用路径===}
#ALIAS {bxh}
{
     mgo 106;
     #delay 2 {buy xionghuang}
}
#ALIAS {bfd}
{
     mgo 276;
     #delay 2 {buy shui dai;buy 50 gan liang}
}

#nop {===基本别名===}
#ALIAS {ff}
{
     exert recover;
     #delay 1 {exert regenerate}
}

#ALIAS {food}
{
     eat gan liang;
     drink shui dai
}

#ALIAS {fullme}
{
     exert recover;
     exert regenerate;
     food
}

#nop {===其他设置===}
#ALIAS {zzz}
{
     #zap
}

#VARIABLE         {placelist}  {{1}{洛阳城}{2}{北京城}{3}{长安城}{4}{开封城}{5}{中州城}{6}{苏州城}{7}{杭州城}{8}{襄阳城}{9}{扬州城}{10}{昆明城}{11}{荆州城}{12}{兰州城}{13}{成都城}{14}{福州城}{15}{灵州城}{16}{武功镇}{17}{泉州城}{18}{华山附近}{19}{凌霄城附近}{20}{五毒教附近}{21}{佛山一带}{22}{南海一带}{23}{汝州一带}{24}{嵩山一带}{25}{终南山}{26}{星宿海}{27}{天山}{28}{关外}{29}{西域}{30}{大理一带}}
#VARIABLE         {placetable}  {{中州城}{{id}{zhongzhou}{path}{zz}{vnum}{3}}{五毒教附近}{{id}{dali}{path}{wdj}{vnum}{19}}{佛山一带}{{id}{foshan}{path}{fs}{vnum}{8}}{兰州城}{{id}{lanzhou}{path}{laz}{vnum}{13}}{关外}{{id}{guanwai}{path}{gw}{vnum}{20}}{凌霄城附近}{{id}{chengdu}{path}{lx}{vnum}{14}}{北京城}{{id}{beijing}{path}{bj}{vnum}{15}}{华山附近}{{id}{huashan}{path}{hs}{vnum}{38}}{南海一带}{{id}{nanhai}{path}{nh}{vnum}{24}}{大理一带}{{id}{dali}{path}{dl}{vnum}{19}}{天山}{{id}{lingjiu}{path}{ts}{vnum}{34}}{嵩山一带}{{id}{nanyang}{path}{ss}{vnum}{16}}{开封城}{{id}{kaifeng}{path}{kf}{vnum}{17}}{成都城}{{id}{chengdu}{path}{cd}{vnum}{14}}{扬州城}{{id}{yangzhou}{path}{yz}{vnum}{1}}{昆明城}{{id}{kunming}{path}{km}{vnum}{18}}{星宿海}{{id}{xingxiu}{path}{xx}{vnum}{45}}{杭州城}{{id}{hangzhou}{path}{hz}{vnum}{12}}{武功镇}{{id}{wugong}{path}{wg}{vnum}{25}}{汝州一带}{{id}{nanyang}{path}{ss}{vnum}{16}}{泉州城}{{id}{quanzhou}{path}{qz}{vnum}{7}}{洛阳城}{{id}{luoyang}{path}{ly}{vnum}{10}}{灵州城}{{id}{lingzhou}{path}{liz}{vnum}{21}}{福州城}{{id}{fuzhou}{path}{fz}{vnum}{6}}{终南山}{{id}{zhongnan}{path}{zn}{vnum}{26}}{苏州城}{{id}{suzhou}{path}{sz}{vnum}{2}}{荆州城}{{id}{jingzhou}{path}{jz}{vnum}{5}}{襄阳城}{{id}{xiangyang}{path}{xy}{vnum}{4}}{西域}{{id}{mingjiao}{path}{xiyu}{vnum}{35}}{长安城}{{id}{changan}{path}{ca}{vnum}{11}}}

#CLASS {quest} CLOSE
