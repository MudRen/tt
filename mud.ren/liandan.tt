#CLASS {liandan} OPEN

#ACTION {回春堂药店内厅}
{
     #if {$step == 1 || $step == 4}
     {
          yaocai
     };
     #elseif {$step == 2}
     {
          goCaiyao
     };
     #elseif {$step == 3}
     {
          caoyao
     }
}
{1}

#ACTION {“啪”的一声一颗仙丹掉到你面前。}
{
     get dan;
     store dan
}

#ACTION {东西到手，快去交了吧。}
{
     cyback
}

#ACTION {你将原料药材一一放进炉中，盘腿坐下，闭目静待。}
{
     #variable step 4
}

#ACTION {你炼成了「%1」。}
{
     finish
}

#ACTION {你炼成了珍品「%1」。}
{
     finish
}

#ACTION {你现在的精神不太好，没法和别人套瓷。}
{
     ldan
}

#ACTION {别在这浪费时间了，快走吧。}
{
     yaocai
}

#ACTION {原料都没有，炼什么啊？}
{
     #variable step 1;
     #delay 1 {#2 s}
}

#ACTION {姚春对你微笑道：炼丹是个修身养性的过程，你能成功，很不错！}
{
     ldan
}

#ACTION {姚春对你点了点头，说道：炼丹是考验人的定力修为，心不静则事不成}
{
     #variable step 1;
     s
}

#ACTION {小童对你说道：好吧，那你去城西的林子里挖几样新鲜的草药来。}
{
     #variable step 2;
     goCaiyao
}

#ACTION {您已经发呆超过%1分钟了，开始研究发呆神功。}
{
     rideto beijing;
     ldan
}

#ACTION {正当你昏昏然的时候，一阵刺鼻的气味从炉中冲出，你急忙开炉取药}
{
     #variable step 1;
     #delay 1 {#2 s}
}

#ACTION {看起来%1想杀死你}
{
     kill du
}

#ALIAS {caiyao}
{
     #ticker caiyao {fullme;cai yao} 5
}

#ALIAS {caoyao}
{
     #delay 1 {give cao yao to xiao tong};
     #delay 1 {#2 n;liandan}
}

#ALIAS {cyback}
{
     #variable step 3;
     #unticker caiyao;
     #map find 回春堂药店内厅;
     #path run 0.5
}

#ALIAS {finish}
{
     #variable step 5;
     store dan;
     fullme;
     ldan
}

#ALIAS {fullme}
{
     exert recover;
     exert regenerate;
     eat gan liang;
     drink shui dai
}

#ALIAS {goCaiyao}
{
     #map find 城西后林;
     #path run 0.5;
     caiyao
}

#ALIAS {ldan}
{
     #map find 姚春内室;
     #path run 0.5;
     #delay 1 {fullme;ask yao chun about liandan}
}

#ALIAS {yaocai}
{
     #map find 回春堂药店内厅;
     #path run 0.5;
     #delay 1 {fullme;ask xiao tong about yaocai}
}


#CLASS {liandan} CLOSE
